USE Nucleo


--REEMPLAZAR VALOR POR LA FECHA (YYYYMMDD) QUE SE DESEA HACER ROLLBACK 
DECLARE @EXEC_DATE CHAR(8) = '20241104' 

DECLARE @MIG TABLE (PERSONA_ID INT, USUARIO_ID INT)
DECLARE @MAXID INT = 0


INSERT INTO @MIG
SELECT TUSE.N_ID_PERSONA, TUSE.N_ID_USUARIO
FROM T_TEMP_USUARIOMAPFRE TEMP 
INNER JOIN T_MAE_USUARIO TUSE ON TUSE.C_COD_USUARIO = TEMP.USUARIO AND TUSE.N_ID_PERSONA = TEMP.PERSONA_ID_GLOBAL AND TEMP.USUARIO_OBS_DATE = @EXEC_DATE
WHERE TEMP.USUARIO_OBS = 'Migrado'
  AND TEMP.CANAL_ID_GLOBAL IS NOT NULL


BEGIN TRY
	BEGIN TRANSACTION

		DELETE TB
		FROM T_MAE_USUARIO_CIA_PRODUCTO_PLAN TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID AND TB.N_ID_COMPANIA = 5 

		DELETE TB
		FROM T_MAE_USUARIO_CANAL_PTOVENTA TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID 
		
		
		DELETE TB
		FROM T_DET_USUARIOCLAVE TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID 
		
		SET @MAXID = ISNULL((SELECT MAX(N_ID_DETUSUARIOCLAVE) FROM T_DET_USUARIOCLAVE), 0)
		DBCC CHECKIDENT([T_DET_USUARIOCLAVE], RESEED, @MAXID)


		DELETE TB
		FROM T_DET_USUARIOPERFIL TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID 

		SET @MAXID = ISNULL((SELECT MAX(N_ID_DETUSUARIOPERFIL) FROM T_DET_USUARIOPERFIL), 0)
		DBCC CHECKIDENT([T_DET_USUARIOPERFIL], RESEED, @MAXID)
		
		
		DELETE TB
		FROM T_DET_USUARIOOPCIONPERFIL TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID 
		
		SET @MAXID = ISNULL((SELECT MAX(N_ID_DETUSUARIOOPCIONPERFIL) FROM T_DET_USUARIOOPCIONPERFIL), 0)
		DBCC CHECKIDENT([T_DET_USUARIOOPCIONPERFIL], RESEED, @MAXID)


		DELETE TB
		FROM T_MAE_USUARIO TB
		INNER JOIN @MIG MIG ON TB.N_ID_USUARIO = MIG.USUARIO_ID AND TB.N_ID_PERSONA = MIG.PERSONA_ID 

		SET @MAXID = ISNULL((SELECT MAX(N_ID_USUARIO) FROM T_MAE_USUARIO), 0)
		DBCC CHECKIDENT([T_MAE_USUARIO], RESEED, @MAXID)


		DELETE TB
		FROM T_DET_PERSONA TB
		INNER JOIN @MIG MIG ON TB.N_ID_PERSONA = MIG.PERSONA_ID 

		SET @MAXID = ISNULL((SELECT MAX(N_ID_DETPERSONA) FROM T_DET_PERSONA), 0)
		DBCC CHECKIDENT([T_DET_PERSONA], RESEED, @MAXID)


		DELETE TB
		FROM T_MAE_PERSONA TB
		INNER JOIN @MIG MIG ON TB.N_ID_PERSONA = MIG.PERSONA_ID 

		SET @MAXID = ISNULL((SELECT MAX(N_ID_PERSONA) FROM T_MAE_PERSONA), 0)
		DBCC CHECKIDENT([T_MAE_PERSONA], RESEED, @MAXID)

		PRINT 'Actualizando USUARIO_OBS...'

		UPDATE T_TEMP_USUARIOMAPFRE
		SET USUARIO_OBS = NULL,
			USUARIO_OBS_DATE = NULL
		WHERE CANAL_ID_GLOBAL IN (6, 0)
		  AND COD_ROL_GLOBAL IS NOT NULL 
		  AND USUARIO_OBS = 'Migrado'
		  AND USUARIO_OBS_DATE = @EXEC_DATE

	COMMIT TRANSACTION
END TRY
BEGIN CATCH
    ROLLBACK TRANSACTION;

    PRINT 'Error: ' + ERROR_MESSAGE(); 

END CATCH;